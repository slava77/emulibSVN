<?xml version="1.0"?>

<xsl:transform 
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
    xmlns:a="http://cms.cern.ch/emu/afeb/teststand/application" 
    xmlns:c="http://cms.cern.ch/emu/afeb/teststand/configuration" 
    xmlns:meta="http://cms.cern.ch/emu/afeb/teststand/metadata" 
    version="1.0">

  <xsl:output method="html" indent="yes"/>

  <xsl:template match="a:application">
    <table class="layout">
      <tr>
	<td>
	  <span class="{@state}"><xsl:value-of select="@state"/></span>
	  <xsl:if test="@state='Failed'"><br/><br/><input type="submit" name="fsm" value="Reset" class="submit" title="Reset application."/></xsl:if>
	</td>
	<td><xsl:copy-of select="a:message"/></td>
	<td><xsl:value-of select="@dateTime"/></td>
      </tr>
      <tr>
	<td colspan="3"><hr/></td>
      </tr>
      <tr>
	<td>
	  <input type="submit" name="fsm" value="Configure" class="submit" title="Configure for the selected mode.">
	    <xsl:if test="@state!='Halted'"><xsl:attribute name="disabled">disabled</xsl:attribute></xsl:if>
	  </input>
	  <select name="mode">
	    <xsl:if test="@state!='Halted'"><xsl:attribute name="disabled">disabled</xsl:attribute></xsl:if>
	    <option title="Perform measurements.">
	      <xsl:if test="@mode='measurement'"><xsl:attribute name="selected">selected</xsl:attribute></xsl:if>
	      measurement
	    </option>
	    <option title="Perform calibrations.">
	      <xsl:if test="@mode='calibration'"><xsl:attribute name="selected">selected</xsl:attribute></xsl:if>
	      calibration
	    </option>
	  </select>
	</td>
	<td>
	  <xsl:choose>
	    <xsl:when test="@mode='calibration'">
	      <input type="submit" name="fsm" class="submit">
		<xsl:choose>
		  <xsl:when test="@state='Enabled'">
		    <xsl:attribute name="value">Stop</xsl:attribute>
		    <xsl:attribute name="title">Stop the calibration to change DAC values.</xsl:attribute>
		  </xsl:when>
		  <xsl:otherwise>
		    <xsl:attribute name="value">Enable</xsl:attribute>
		    <xsl:attribute name="title">Start the calibration with the given DAC values.</xsl:attribute>
		  </xsl:otherwise>
		</xsl:choose>
		<xsl:if test="@state='Halted' or @state='Failed'"><xsl:attribute name="disabled">disabled</xsl:attribute></xsl:if>
	      </input>
	    </xsl:when>
	    <xsl:otherwise>
	      <input type="submit" name="fsm" value="Enable" class="submit" title="Start the measurement.">
		<xsl:if test="@state!='Configured'"><xsl:attribute name="disabled">disabled</xsl:attribute></xsl:if>
	      </input>
	    </xsl:otherwise>
	  </xsl:choose>
	</td>
	<td>
	  <input type="submit" name="fsm" value="Halt" class="submit">
	    <xsl:if test="@mode='calibration'"><xsl:attribute name="title">Halt the calibration to reconfigure the setup.</xsl:attribute></xsl:if>
	    <xsl:if test="@mode='measurement'"><xsl:attribute name="title">Halt the measurement.</xsl:attribute></xsl:if>
	    <xsl:if test="@state='Halted' or @state='Failed'"><xsl:attribute name="disabled">disabled</xsl:attribute></xsl:if>
	  </input>
	</td>
      </tr>
      <tr>
	<td colspan="3">
	  <hr/>
	  <xsl:if test="$APPMODE='measurement'"><xsl:apply-templates mode="application" select="../a:results"/></xsl:if>
	  <xsl:if test="$APPMODE='calibration'"><xsl:apply-templates select="a:calibration"/></xsl:if>
	</td>
      </tr>
      <xsl:if test="@state='Halted'">
	<tr>
	  <td colspan="3">
	    Select a previously defined configuration:
	    <xsl:apply-templates select="a:configuration"/>
	    <input type="submit" name="config" value="load" title="Load the selected configuration." class="submit"/>
	  </td>
	</tr>
      </xsl:if>
    </table>
  </xsl:template>

  <xsl:template mode="application" match="a:results">
    <xsl:variable name="NDEVICES" select="count(a:measurement[position()=1]/a:device)"/>
    <xsl:if test="$NDEVICES != 0">
      <table>
	<tr>
	  <th colspan="{number($NDEVICES)+3}">measurement</th>
	</tr>      
	<tr>
	  <th>index</th><th>type</th><th>name</th><th colspan="{$NDEVICES}">devices</th>
	</tr>
	<xsl:apply-templates mode="application" select="a:measurement">
	  <xsl:sort select="@index"/>
	</xsl:apply-templates>
      </table>
    </xsl:if>
    <xsl:for-each select="a:measurement[@status='running']/a:device">
      <xsl:sort select="@id"/>
      <img src="{../../@urlPath}{a:file/@name}.png"/>
    </xsl:for-each>
  </xsl:template>

  <xsl:template mode="application" match="a:measurement">
    <tr>
      <td><xsl:value-of select="@index"/></td>
      <td><xsl:value-of select="@type"/></td>
      <td><xsl:value-of select="@name"/></td>
      <xsl:apply-templates mode="application" select="a:device">
	<xsl:sort select="@id"/>
      </xsl:apply-templates>
    </tr>
  </xsl:template>

  <xsl:template mode="application" match="a:device">
    <td>
      <a href="{/root/a:application/@urlPath}/results#{a:file/@name}" target="_blank">
	<!-- Indicate whether this measurement of this device passed the selection cuts -->
	<xsl:attribute name="class">
	  <xsl:value-of select="../@status"/>
	  <xsl:choose>
	    <xsl:when test="../@type='count_vs_dac'">
	      <xsl:if test="number(a:statistics/a:parameter[@name='threshold [DAC units]']/@maxAbsResid) >= number(//c:configuration/c:selection/c:thresholdAbsResid/@high) or number(a:statistics/a:parameter[@name='noise [DAC units]']/@max) >= number(//c:configuration/c:selection/c:noise/@high)"> failed</xsl:if>
	    </xsl:when>	    
	    <xsl:otherwise>
	      <xsl:if test="number(a:statistics/a:parameter[starts-with(@name,'span of channels')]/@max) >= number(//c:configuration/c:selection/c:meanTimeSpan/@high) or number(a:statistics/a:parameter[@name='slewing time on plateau [TDC units]']/@max) >= number(//c:configuration/c:selection/c:slewingTime/@high)"> failed</xsl:if>
	    </xsl:otherwise>
	  </xsl:choose>
	</xsl:attribute>
	<xsl:value-of select="@id"/>
      </a>
    </td>
  </xsl:template>

  <xsl:template mode="results" match="a:results">
    <p>
      These results can also be accessed offline by opening <a href="file://{@systemPath}/{@file}">file://<xsl:value-of select="@systemPath"/>/<xsl:value-of select="@file"/></a> in a browser running on host <xsl:value-of select="@host"/>.
    </p>
    <table class="layout">
      <xsl:apply-templates mode="results" select="a:measurement/a:device">
	<xsl:sort select="../@index"/>
 	<xsl:sort select="@id"/>
      </xsl:apply-templates>
   </table>
  </xsl:template>

  <xsl:template mode="results" match="a:device">
    <tr>
      <td colspan="2">
	<table class="title">
	  <tr><th colspan="3">measurement<a id="{../@index}__{../@type}__{@id}"/></th><th>device</th></tr>
	  <tr><th>index</th><th>type</th><th>name</th><th>id</th></tr>
	  <tr><td><xsl:value-of select="../@index"/></td><td><xsl:value-of select="../@type"/></td><td><xsl:value-of select="../@name"/></td><td><xsl:value-of select="@id"/></td></tr>
	</table>
      </td>
    </tr>
    <tr>
      <td>
	<a name="{a:file/@name}"/>
	<img src="{../../@urlPath}{a:file/@name}.png"/>
      </td>
      <td style="vertical-align:top;">
	<table>
	  <xsl:apply-templates mode="header" select="a:channel[@number=1]"/>
	  <xsl:apply-templates mode="data" select="a:channel"/>
	</table>
	<xsl:apply-templates select="a:statistics"/>
      </td>
    </tr>
  </xsl:template>

  <xsl:template mode="header" match="a:channel">
      <tr>
	<th rowspan="2">channel</th>
	<xsl:for-each select="a:parameter">
	  <xsl:choose>
	    <xsl:when test="@name='chi2ndf'">
	      <th rowspan="2">&#x03c7;&#xb2;/ndf</th>
	    </xsl:when>
	    <xsl:when test="@name='RMS of times on plateau [TDC]'">
	      <th rowspan="2"><xsl:value-of select="@name"/></th>
	    </xsl:when>
	    <xsl:otherwise>
	      <th colspan="2"><xsl:value-of select="@name"/></th>
	    </xsl:otherwise>
	  </xsl:choose>
	</xsl:for-each>
      </tr>
      <tr>
	<xsl:for-each select="a:parameter">
	  <xsl:choose>
	    <xsl:when test="@name='chi2ndf'">
	    </xsl:when>
	    <xsl:when test="@name='RMS of times on plateau [TDC]'">
	    </xsl:when>
	    <xsl:otherwise>
	      <th>value</th><th>error</th>
	    </xsl:otherwise>
	  </xsl:choose>
	</xsl:for-each>
      </tr>
  </xsl:template>

  <xsl:template mode="data" match="a:channel">
      <tr>
	<th><xsl:value-of select="@number"/></th>
	<xsl:for-each select="a:parameter">
	  <!-- <td style="width: 25x;"><xsl:value-of select="@value"/> &#177; <xsl:value-of select="@error"/></td> -->
	  <td><xsl:value-of select="@value"/></td>
	  <xsl:if test="@name!='chi2ndf' and @name!='RMS of times on plateau [TDC]'">
	    <td><xsl:value-of select="@error"/></td>
	  </xsl:if>
	</xsl:for-each>
      </tr>
  </xsl:template>

  <xsl:template match="a:statistics">
    <table>
      <tr><th colspan="{count(a:statistics/a:parameter[position=1]/@*)}">Uniformity of channels</th></tr>
      <xsl:apply-templates mode="header" select="a:parameter[position()=1]"/>
      <xsl:apply-templates mode="data" select="a:parameter"/>
    </table>
  </xsl:template>

  <xsl:template mode="header" match="a:parameter">
      <tr>
	<th>parameter name</th>
	<xsl:for-each select="@*[local-name()!='name']">
	  <th style="width: 25x;"><xsl:value-of select="local-name()"/></th>
	</xsl:for-each>
      </tr>
  </xsl:template>

  <xsl:template mode="data" match="a:parameter">
      <tr>
	<th><xsl:value-of select="@name"/></th>
	<xsl:apply-templates mode="data" select="@*[local-name()!='name']"/>
      </tr>
  </xsl:template>

  <!-- No cut -->
  <xsl:template mode="data" match="@*">
    <td style="width: 25x;"><xsl:value-of select="."/></td>
  </xsl:template>

  <!-- Cut on thresholdAbsResid -->
  <xsl:template mode="data" match="@maxAbsResid[../@name='threshold [DAC units]']">
    <xsl:param name="RESULT">
      <xsl:choose>
	<xsl:when test="number(//c:configuration/c:selection/c:thresholdAbsResid/@high) > number(.)">passed</xsl:when>
	<xsl:otherwise>failed</xsl:otherwise>
      </xsl:choose>
    </xsl:param>
    <td style="width: 25x;" class="{$RESULT}">
      <xsl:attribute name="title"><xsl:value-of select="//meta:data/meta:tag[@name='c:thresholdAbsResid']/@description"/> It <xsl:value-of select="$RESULT"/> the selection cut.</xsl:attribute>
      <xsl:value-of select="."/>
    </td>
  </xsl:template>

  <!-- Cut on noise -->
  <xsl:template mode="data" match="@max[../@name='noise [DAC units]']">
    <xsl:param name="RESULT">
      <xsl:choose>
  	<xsl:when test="number(//c:configuration/c:selection/c:noise/@high) > number(.)">passed</xsl:when>
  	<xsl:otherwise>failed</xsl:otherwise>
      </xsl:choose>
    </xsl:param>
    <td style="width: 25x;" class="{$RESULT}">
      <xsl:attribute name="title"><xsl:value-of select="//meta:data/meta:tag[@name='c:noise']/@description"/> It <xsl:value-of select="$RESULT"/> the selection cut.</xsl:attribute>
      <xsl:value-of select="."/>
    </td>
  </xsl:template>

  <!-- Cut on meanTimeSpan -->
  <!-- <xsl:template mode="data" match="@max[../@name='span of channels&#39; mean times [TDC units]']"> -->
  <!-- <xsl:template mode="data" match="@max[../@name='span of channels&apos; mean times [TDC units]']"> -->
  <!-- Avoid single quote in name to make XSLT happy. -->
  <xsl:template mode="data" match="@max[starts-with(../@name,'span of channels')]">
    <xsl:param name="RESULT">
      <xsl:choose>
  	<xsl:when test="number(//c:configuration/c:selection/c:meanTimeSpan/@high) > number(.)">passed</xsl:when>
  	<xsl:otherwise>failed</xsl:otherwise>
      </xsl:choose>
    </xsl:param>
    <td style="width: 25x;" class="{$RESULT}">
      <xsl:attribute name="title"><xsl:value-of select="//meta:data/meta:tag[@name='c:meanTimeSpan']/@description"/> It <xsl:value-of select="$RESULT"/> the selection cut.</xsl:attribute>
      <xsl:value-of select="."/>
    </td>
  </xsl:template>

  <!-- Cut on slewingTime -->
  <xsl:template mode="data" match="@max[../@name='slewing time on plateau [TDC units]']">
    <xsl:param name="RESULT">
      <xsl:choose>
  	<xsl:when test="number(//c:configuration/c:selection/c:slewingTime/@high) > number(.)">passed</xsl:when>
  	<xsl:otherwise>failed</xsl:otherwise>
      </xsl:choose>
    </xsl:param>
    <td style="width: 25x;" class="{$RESULT}">
      <xsl:attribute name="title"><xsl:value-of select="//meta:data/meta:tag[@name='c:slewingTime']/@description"/> It <xsl:value-of select="$RESULT"/> the selection cut.</xsl:attribute>
      <xsl:value-of select="."/>
    </td>
  </xsl:template>

  <xsl:template match="a:configuration">
    <select name="file" size="3" title="Select a configuration file to load.">
      <xsl:apply-templates select="a:file"/>
    </select>
  </xsl:template>

  <xsl:template match="a:file">
    <option value="{@name}"><xsl:value-of select="@time"/><xsl:text> </xsl:text><b><xsl:value-of select="@name"/></b></option>
  </xsl:template>

  <xsl:template match="a:calibration">
    <p>Enter the DAC values, and click 'Enable' to set them.</p>
    <table class="calibrations">
      <tr><th>all DACs of type</th><th>value [DAC units]</th></tr>
      <tr><td>test pulse</td><td><input type="text" name="pulseApmlitude" value="{@pulseAmplitude}" title="Enter a negative integer if test pulse DACs are not to be pulsed." size="4"/></td></tr>
      <tr><td>threshold</td><td><input type="text" name="thresholdLevel" value="{@thresholdLevel}" title="Enter a negative integer if threshold DACs are not to be set." size="4"/></td></tr>
    </table>
  </xsl:template>

</xsl:transform>
