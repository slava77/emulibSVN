<?xml version="1.0"?>

<xsl:transform 
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
    xmlns:a="http://cms.cern.ch/emu/afeb/teststand/application" 
    version="1.0">

  <xsl:output method="html" indent="yes"/>

  <xsl:template match="a:application">
    <table class="layout">
      <tr>
	<td><xsl:value-of select="@a:state"/></td>
	<td/>
	<td><xsl:value-of select="@a:dateTime"/></td>
      </tr>
      <tr>
	<td>
	  <input type="submit" name="fsm" value="Configure" class="submit" title="Configure the measurement.">
	    <xsl:if test="@a:state!='Halted'"><xsl:attribute name="disabled">disabled</xsl:attribute></xsl:if>
	  </input>
	</td>
	<td>
	  <input type="submit" name="fsm" value="Enable" class="submit" title="Start the measurement.">
	    <xsl:if test="@a:state!='Configured'"><xsl:attribute name="disabled">disabled</xsl:attribute></xsl:if>
	  </input>
	</td>
	<td>
	  <input type="submit" name="fsm" value="Halt" class="submit" title="Halt the measurement.">
	    <xsl:if test="@a:state='Halted'"><xsl:attribute name="disabled">disabled</xsl:attribute></xsl:if>
	  </input>
	</td>
      </tr>
      <tr>
	<td colspan="3">
	  <xsl:apply-templates mode="application" select="../a:results"/>
	</td>
      </tr>
      <xsl:if test="@a:state='Halted'">
	<tr>
	  <td colspan="3">
	    Select a previously defined configuration:
	    <xsl:apply-templates select="a:configuration"/>
	    <input type="submit" name="config" value="load" title="Load the selected configuration." class="submit"/>
	  </td>
	</tr>
      </xsl:if>
    </table>
  </xsl:template>

  <xsl:template mode="application" match="a:results">
    <p>
      <xsl:apply-templates mode="application" select="a:device">
    	<xsl:sort select="a:plot/@a:name"/>
      </xsl:apply-templates>
    </p>
    <xsl:for-each select="a:device[@a:status='running']">
      <img src="{../@a:urlPath}{a:plot/@a:name}.png"/>
    </xsl:for-each>
  </xsl:template>

  <xsl:template mode="results" match="a:results">
    <table class="layout">
      <xsl:apply-templates mode="results" select="a:device"/>
    </table>
  </xsl:template>

  <xsl:template mode="application" match="a:device">
    <a href="{/root/a:application/@a:urlPath}/results" class="{@a:status}" target="_blank"><xsl:value-of select="a:plot/@a:name"/></a><xsl:if test="position()!=last()"> | </xsl:if>
  </xsl:template>

  <xsl:template mode="results" match="a:device">
    <tr>
      <td>
	<a name="{a:plot/@a:name}"/>
	<img src="{../@a:urlPath}{a:plot/@a:name}.png"/>
      </td>
      <table>
	<xsl:apply-templates mode="header" select="a:channel[@a:number=1]"/>
	<xsl:apply-templates mode="data" select="a:channel"/>
      </table>
    </tr>
  </xsl:template>

  <xsl:template mode="header" match="a:channel">
      <tr>
	<th>channel</th>
	<xsl:for-each select="a:parameter">
	  <th><xsl:value-of select="@a:name"/></th>
	</xsl:for-each>
      </tr>
  </xsl:template>

  <xsl:template mode="data" match="a:channel">
      <tr>
	<th><xsl:value-of select="@a:number"/></th>
	<xsl:for-each select="a:parameter">
	  <td style="width: 25x;"><xsl:value-of select="@a:value"/>&#177;<xsl:value-of select="@a:error"/></td>
	</xsl:for-each>
      </tr>
  </xsl:template>

  <xsl:template match="a:configuration">
    <select name="file" size="3" title="Select a configuration file to load.">
      <xsl:apply-templates select="a:file"/>
    </select>
  </xsl:template>

  <xsl:template match="a:file">
    <option value="{@a:name}"><xsl:value-of select="@a:time"/><xsl:text> </xsl:text><b><xsl:value-of select="@a:name"/></b></option>
  </xsl:template>

</xsl:transform>
