<?xml version="1.0"?>

<xsl:transform 
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
    xmlns:a="http://cms.cern.ch/emu/afeb/teststand/application" 
    version="1.0">

  <xsl:output method="html" indent="yes"/>

  <xsl:template match="//a:application">
    <table class="layout">
      <tr>
	<td><xsl:value-of select="@a:state"/></td>
	<td/>
	<td><xsl:value-of select="@a:dateTime"/></td>
      </tr>
      <tr>
	<td>
	  <input type="submit" name="fsm" value="Configure" class="submit" title="Configure the measurement.">
	    <xsl:if test="@a:state!='Halted'"><xsl:attribute name="disabled">disabled</xsl:attribute></xsl:if>
	  </input>
	</td>
	<td>
	  <input type="submit" name="fsm" value="Enable" class="submit" title="Start the measurement.">
	    <xsl:if test="@a:state!='Configured'"><xsl:attribute name="disabled">disabled</xsl:attribute></xsl:if>
	  </input>
	</td>
	<td>
	  <input type="submit" name="fsm" value="Halt" class="submit" title="Halt the measurement.">
	    <xsl:if test="@a:state='Halted'"><xsl:attribute name="disabled">disabled</xsl:attribute></xsl:if>
	  </input>
	</td>
      </tr>
      <tr>
	<td colspan="3">
	  <xsl:apply-templates select="//a:results"/>
	</td>
      </tr>
      <xsl:if test="@a:state='Halted'">
	<tr>
	  <td colspan="3">
	    <xsl:apply-templates select="//a:configuration"/>
	    <input type="submit" name="config" value="load" title="Load the selected configuration." class="submit"/>
	  </td>
	</tr>
      </xsl:if>
    </table>
  </xsl:template>  

  <xsl:template match="a:results">
    <p>
      <xsl:apply-templates select="a:plot">
	<xsl:sort select="@a:name"/>
      </xsl:apply-templates>
    </p>
    <xsl:for-each select="a:plot[@a:current='yes']">
      <img src="{@a:url}"/>
    </xsl:for-each>
  </xsl:template>

  <xsl:template match="a:plot">
    <a href="{@a:url}" class="popup" target="_blank"><xsl:value-of select="@a:name"/></a><xsl:if test="position()!=last()"> | </xsl:if>
  </xsl:template>

  <xsl:template match="a:configuration">
    <select name="file" size="3" title="Select a configuration file to load.">
      <xsl:apply-templates select="a:file"/>
    </select>
  </xsl:template>

  <xsl:template match="a:file">
    <option value="{@a:name}"><xsl:value-of select="@a:time"/><xsl:text> </xsl:text><b><xsl:value-of select="@a:name"/></b></option>
  </xsl:template>

</xsl:transform>
