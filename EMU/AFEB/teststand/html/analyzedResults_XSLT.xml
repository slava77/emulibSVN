<?xml version="1.0"?>

<xsl:transform 
    xmlns:xsl="http://www.w3.org/1999/XSL/Transform" 
    xmlns:ad="http://cms.cern.ch/emu/afeb/teststand/analyzeddevice"
    xmlns:meta="http://cms.cern.ch/emu/afeb/teststand/metadata"
    version="1.0">

  <xsl:output method="html" indent="yes"/>

  <xsl:template mode="description" match="@*">
    <xsl:param name="TAG"><xsl:value-of select="name(..)"/></xsl:param>
    <xsl:param name="ATTR"><xsl:value-of select="name(.)"/></xsl:param>
    <xsl:param name="VALUE"><xsl:value-of select="."/></xsl:param>
    <xsl:value-of select="//meta:data/meta:tag[@name=$TAG]/meta:attr[@name=$ATTR and ( not(@value) or @value=$VALUE )]/@description"/>
  </xsl:template>

  <xsl:template mode="units" match="@*">
    <xsl:param name="TAG"><xsl:value-of select="name(..)"/></xsl:param>
    <xsl:param name="ATTR"><xsl:value-of select="name(.)"/></xsl:param>
    <xsl:value-of select="//meta:data/meta:tag[@name=$TAG]/meta:attr[@name=$ATTR]/@units"/>
  </xsl:template>

  <xsl:template match="/ad:device">
    <html xmlns="http://www.w3.org/1999/xhtml">
      <head>
	<title><xsl:value-of select="@id"/> (<xsl:value-of select="@type"/>): analyzed results</title>
	<style type="text/css">
	  table,td,th { border-color: #000000; border-collapse: collapse; border-width: 1px 1px 1px 1px; border-style:solid; }
	  th,td { padding: 5px 5px 5px 5px; }
	  table.channel { width: 100%; margin-top: 5px; }
	  table.statistics { width: 100%; }
	  table.adaptor { padding: 5px 5px 5px 5px; margin: 5px 5px 5px 5px; }
	  table.measurement { padding: 5px 5px 5px 5px; margin: 5px 5px 5px 5px; padding: 0px 0px 0px 0px; }
	  table.device { padding: 5px 5px 5px 5px; margin: 5px 5px 5px 5px; }
	  td { text-align: right; color: #0000ff; }
	  th { color: #000000; }
	  .highlight { color: #0000ff; }	  
	</style>
      </head>
      <body>
	<p><h1>Device <span class="highlight"><xsl:value-of select="@id"/></span> of type <xsl:value-of select="@type"/></h1></p>
	<p><h3>Results of analysis of <xsl:value-of select="@analysisDate"/> of device <xsl:value-of select="@id"/> of type <xsl:value-of select="@type"/> measured at <xsl:value-of select="@measurementDate"/></h3></p>

	<xsl:apply-templates select="*[name()!='ad:channel']"/>

	<table class="device">
	  <tr>
	    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@number"/></xsl:attribute>channel</th>
	    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@offset"/></xsl:attribute>offset [<xsl:apply-templates mode="units" select="ad:channel[1]/@offset"/>]</th>
	    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@gain"/></xsl:attribute>gain [<xsl:apply-templates mode="units" select="ad:channel[1]/@gain"/>]</th>
	    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@C_int"/></xsl:attribute>C<span style="vertical-align:sub;">internal</span> [<xsl:apply-templates mode="units" select="ad:channel[1]/@C_int"/>]</th>
	    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@averageNoise"/></xsl:attribute>average noise [<xsl:apply-templates mode="units" select="ad:channel[1]/@averageNoise"/>]</th>
	  </tr>
	  <xsl:apply-templates select="ad:channel"/>
	</table>

	<table class="device">
	  <tr><th></th><xsl:apply-templates mode="th" select="ad:noise"/></tr>
	  <tr><th>noise</th><xsl:apply-templates mode="td" select="ad:noise"/></tr>
	</table>

	<table class="device">
	  <tr><th></th><xsl:apply-templates mode="th" select="ad:gain"/></tr>
	  <tr><th>gain</th><xsl:apply-templates mode="td" select="ad:gain"/></tr>
	</table>

	<table class="device">
	  <tr><th></th><xsl:apply-templates mode="th" select="ad:offset"/></tr>
	  <tr><th>offset</th><xsl:apply-templates mode="td" select="ad:offset"/></tr>
	</table>

	<table class="device">
	  <tr><th></th><xsl:apply-templates mode="th" select="ad:C_int"/></tr>
	  <tr><th>C<span style="vertical-align:sub;">internal</span></th><xsl:apply-templates mode="td" select="ad:C_int"/></tr>
	</table>

	<table class="device">
	  <tr>
	    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:averageSetThreshold/@value"/></xsl:attribute>average set threshold [<xsl:apply-templates mode="units" select="ad:averageSetThreshold/@value"/>] at input charge of <xsl:value-of select="ad:averageSetThreshold/@atCharge"/> <xsl:apply-templates mode="units" select="ad:averageSetThreshold/@atCharge"/></th>
	    <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:averageSetThreshold/@value"/></xsl:attribute><xsl:value-of select="ad:averageSetThreshold/@value"/></td>
	  </tr>
	  <tr>
	    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:maxMeasuredThreshold/@value"/></xsl:attribute>maximum measured threshold  [<xsl:apply-templates mode="units" select="ad:maxMeasuredThreshold/@value"/>] at set threshold of <xsl:value-of select="ad:maxMeasuredThreshold/@atSetThreshold"/> <xsl:apply-templates mode="units" select="ad:maxMeasuredThreshold/@atSetThreshold"/></th>
	    <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:maxMeasuredThreshold/@value"/></xsl:attribute><xsl:value-of select="ad:maxMeasuredThreshold/@value"/></td>
	  </tr>
	</table>

      </body>
    </html>
  </xsl:template>

  <xsl:template match="ad:adaptor">
    <table class="adaptor">
      <tr><th colspan="2">Adaptor</th></tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@id"/></xsl:attribute>id</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@id"/></xsl:attribute><xsl:value-of select="@id"/></td>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@type"/></xsl:attribute>type</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@type"/></xsl:attribute><xsl:value-of select="@type"/></td>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@socket"/></xsl:attribute>socket</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@socket"/></xsl:attribute><xsl:value-of select="@socket"/></td>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@correctionCoefficient"/></xsl:attribute>coefficient</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@correctionCoefficient"/></xsl:attribute><xsl:value-of select="@correctionCoefficient"/></td>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@injectionCapacitance"/></xsl:attribute>C<span style="vertical-align:sub;">input</span> [<xsl:apply-templates mode="units" select="@injectionCapacitance"/>]</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@injectionCapacitance"/></xsl:attribute><xsl:value-of select="@injectionCapacitance"/></td>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@pulseDivisionFactor"/></xsl:attribute>attenuation</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@pulseDivisionFactor"/></xsl:attribute><xsl:value-of select="@pulseDivisionFactor"/></td>
      </tr>
    </table>
  </xsl:template>  
 
  <xsl:template match="ad:measurement">
    <table class="measurement">
      <tr><th colspan="2">Measurement</th></tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@index"/></xsl:attribute>index</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@index"/></xsl:attribute><xsl:value-of select="@index"/></td>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@type"/></xsl:attribute>type</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@type"/></xsl:attribute><xsl:value-of select="@type"/></td>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@capacitor"/></xsl:attribute>capacitor</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@capacitor"/></xsl:attribute><xsl:value-of select="@capacitor"/></td>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@nPulses"/></xsl:attribute>number of pulses</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@nPulses"/></xsl:attribute><xsl:value-of select="@nPulses"/></td>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@setThreshold"/></xsl:attribute>set threshold [<xsl:apply-templates mode="units" select="@setThreshold"/>]</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@setThreshold"/></xsl:attribute><xsl:value-of select="@setThreshold"/></td>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@setThresholdVoltage"/></xsl:attribute>set threshold [<xsl:apply-templates mode="units" select="@setThresholdVoltage"/>]</th>
	<td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@setThresholdVoltage"/></xsl:attribute><xsl:value-of select="@setThresholdVoltage"/></td>
      </tr>
      <tr>
	<td colspan="2">
	  <xsl:if test="@type='count_vs_dac'">
	    <table class="channel">
	      <tr>
		<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@number"/></xsl:attribute>channel</th>
		<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@threshold"/></xsl:attribute>threshold [<xsl:apply-templates mode="units" select="ad:channel[1]/@threshold"/>]</th>
		<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@noise"/></xsl:attribute>noise [<xsl:apply-templates mode="units" select="ad:channel[1]/@noise"/>]</th>
		<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@chi2ndf"/></xsl:attribute>&#x03c7;&#xb2;/ndf</th>
	      </tr>
	      <xsl:apply-templates select="ad:channel"/>
	    </table>
	  </xsl:if>
	  <xsl:if test="@type='time_vs_dac'">
	    <xsl:apply-templates/>
	  </xsl:if>
	</td>
      </tr>
    </table>
  </xsl:template>
 
  <xsl:template match="ad:times">
    <table class="channel">
      <tr>
	<th colspan="3">Times at input charge <xsl:value-of select="@nominalInputCharge"/><xsl:text> </xsl:text><xsl:apply-templates mode="units" select="@nominalInputCharge"/> (actual <xsl:value-of select="@realInputCharge"/><xsl:text> </xsl:text><xsl:apply-templates mode="units" select="@realInputCharge"/>)</th>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@number"/></xsl:attribute>channel</th>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@mean"/></xsl:attribute>mean [<xsl:apply-templates mode="units" select="ad:channel[1]/@mean"/>]</th>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@rms"/></xsl:attribute>rms [<xsl:apply-templates mode="units" select="ad:channel[1]/@rms"/>]</th>
      </tr>
      <xsl:apply-templates select="ad:channel"/>
    </table>   
    <table class="statistics">
      <tr><th>statistics</th><xsl:apply-templates mode="th" select="ad:mean"/></tr>
      <tr><th>mean times</th><xsl:apply-templates mode="td" select="ad:mean"/></tr>
    </table>
    <table class="statistics">
      <tr><th>statistics</th><xsl:apply-templates mode="th" select="ad:rms"/></tr>
      <tr><th>rms of times</th><xsl:apply-templates mode="td" select="ad:rms"/></tr>
    </table>
  </xsl:template>

  <xsl:template match="ad:slew">
    <table class="channel">
      <tr>
	<th colspan="3">Slew between input charge <xsl:value-of select="@fromInputCharge"/><xsl:text> </xsl:text><xsl:apply-templates mode="units" select="@fromInputCharge"/> and <xsl:value-of select="@toInputCharge"/><xsl:text> </xsl:text><xsl:apply-templates mode="units" select="@toInputCharge"/></th>
      </tr>
      <tr>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@number"/></xsl:attribute>channel</th>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@spanOfMeans"/></xsl:attribute>mean [<xsl:apply-templates mode="units" select="ad:channel[1]/@spanOfMeans"/>]</th>
	<th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="ad:channel[1]/@spanOfRMSs"/></xsl:attribute>rms [<xsl:apply-templates mode="units" select="ad:channel[1]/@spanOfRMSs"/>]</th>
      </tr>
      <xsl:apply-templates select="ad:channel"/>
    </table>   
    <table class="statistics">
      <tr><th>statistics</th><xsl:apply-templates mode="th" select="ad:spanOfMeans"/></tr>
      <tr><th>span of means</th><xsl:apply-templates mode="td" select="ad:spanOfMeans"/></tr>
    </table>
    <table class="statistics">
      <tr><th>statistics</th><xsl:apply-templates mode="th" select="ad:spanOfRMSs"/></tr>
      <tr><th>span of RMSs</th><xsl:apply-templates mode="td" select="ad:spanOfRMSs"/></tr>
    </table>
  </xsl:template>

  <xsl:template match="ad:channel[name(..)='ad:measurement']">
    <tr>
      <th><xsl:value-of select="@number"/></th>
      <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@threshold"/></xsl:attribute><xsl:value-of select="@threshold"/></td>
      <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@noise"/></xsl:attribute><xsl:value-of select="@noise"/></td>
      <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@chi2ndf"/></xsl:attribute><xsl:value-of select="@chi2ndf"/></td>
    </tr>
  </xsl:template>

  <xsl:template match="ad:channel[name(..)='ad:times']">
    <tr>
      <th><xsl:value-of select="@number"/></th>
      <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@mean"/></xsl:attribute><xsl:value-of select="@mean"/></td>
      <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@rms"/></xsl:attribute><xsl:value-of select="@rms"/></td>
    </tr>
  </xsl:template>

  <xsl:template match="ad:channel[name(..)='ad:slew']">
    <tr>
      <th><xsl:value-of select="@number"/></th>
      <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@spanOfMeans"/></xsl:attribute><xsl:value-of select="@spanOfMeans"/></td>
      <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@spanOfRMSs"/></xsl:attribute><xsl:value-of select="@spanOfRMSs"/></td>
    </tr>
  </xsl:template>

  <xsl:template match="ad:channel[name(..)='ad:device']">
    <tr>
      <th><xsl:value-of select="@number"/></th>
      <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@offset"/></xsl:attribute><xsl:value-of select="@offset"/></td>
      <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@gain"/></xsl:attribute><xsl:value-of select="@gain"/></td>
      <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@C_int"/></xsl:attribute><xsl:value-of select="@C_int"/></td>
      <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@averageNoise"/></xsl:attribute><xsl:value-of select="@averageNoise"/></td>
    </tr>
  </xsl:template>

  <xsl:template mode="th" match="ad:mean | ad:rms | ad:spanOfMeans | ad:spanOfRMSs | ad:noise | ad:gain | ad:offset | ad:C_int">
    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@min"/></xsl:attribute>minimum</th>
    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@mean"/></xsl:attribute>mean</th>
    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@max"/></xsl:attribute>maximum</th>
    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@rms"/></xsl:attribute>rms</th>
    <th><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@maxAbsResid"/></xsl:attribute>max |residual|</th>
  </xsl:template>

  <xsl:template mode="td" match="ad:mean | ad:rms | ad:spanOfMeans | ad:spanOfRMSs | ad:noise | ad:gain | ad:offset | ad:C_int">
    <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@min"/></xsl:attribute><xsl:value-of select="@min"/></td>
    <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@mean"/></xsl:attribute><xsl:value-of select="@mean"/></td>
    <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@max"/></xsl:attribute><xsl:value-of select="@max"/></td>
    <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@rms"/></xsl:attribute><xsl:value-of select="@rms"/></td>
    <td><xsl:attribute name="title"><xsl:apply-templates mode="description" select="@maxAbsResid"/></xsl:attribute><xsl:value-of select="@maxAbsResid"/></td>
  </xsl:template>

</xsl:transform>
